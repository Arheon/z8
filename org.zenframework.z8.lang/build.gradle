apply plugin: 'java'

eclipse {
	project.natures 'org.zenframework.z8.pde.ProjectNature'
}

configurations {
	compiler
	blzip
}

dependencies {
	implementation project(':org.zenframework.z8.server')

	compiler project(':org.zenframework.z8.compiler')
}

task compileBl(type: JavaExec) {
	group = 'Build'
	description = 'Compile BL sources'

	classpath = configurations.compiler
	main = 'org.zenframework.z8.compiler.cmd.Main'
	args = [ projectDir, "-projectName:${project.name}", "-output:${projectDir}/.java" ]
}

sourceSets {
	main.java.srcDirs "${projectDir}/.java"
}

tasks.withType(JavaCompile) {
	dependsOn compileBl
}

task assembleBlzip(type: Zip, group: 'build', dependsOn: [compileBl]) {
	archiveName "${project.name}-bl.jar"
	destinationDir file("${buildDir}/libs/")
	from(projectDir) {
		include '**/*.bl'
		includeEmptyDirs = false
	}
	description "Assemble archive ${archiveName} into ${relativePath(destinationDir)}"
}

assemble.dependsOn assembleBlzip

artifacts {
	blzip assembleBlzip
}

publishing.publications {
	maven(MavenPublication) {
		from components.java
	}
	mavenBl(MavenPublication) {
		artifact source: assembleBlzip, extension: 'jar', classifier: 'bl'
	}
}
