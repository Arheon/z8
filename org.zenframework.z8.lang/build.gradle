apply plugin: 'java'

eclipse {
	project {
		natures 'org.zenframework.z8.pde.ProjectNature'
		buildCommand 'org.zenframework.z8.pde.ProjectBuilder', JavaSource: "$buildFolder/.java"
	}
}

configurations {
	compiler
	blzip
}

dependencies {
	implementation project(':org.zenframework.z8.server')

	compiler project(':org.zenframework.z8.compiler')
}

task compileBl(type: JavaExec) {
	group = 'Build'
	description = 'Compile BL sources'

	classpath = configurations.compiler
	main = 'org.zenframework.z8.compiler.cmd.Main'
	args = [ projectDir, "-projectName:${project.name}", "-output:${buildDir}/.java" ]
}

sourceSets {
	main.java.srcDirs "${buildDir}/.java"
}

tasks.withType(JavaCompile) {
	dependsOn compileBl
}

task assembleBlzip(type: Zip, group: 'bl', dependsOn: [compileBl]) {
	archiveName "${project.name}.zip"
	destinationDir file("$buildDir/libs/")
	from(projectDir) {
		include '**/*.bl'
		includeEmptyDirs = false
	}
	description "Assemble archive $archiveName into ${relativePath(destinationDir)}"
}

assemble.dependsOn assembleBlzip

artifacts {
	blzip assembleBlzip
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact source: assembleBlzip, extension: 'zip', classifier: 'bl'
		}
	}
}
